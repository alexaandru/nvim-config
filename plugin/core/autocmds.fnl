(let [group (vim.api.nvim_create_augroup :Setup {:clear true})
      ts-start #(let [_ (pcall vim.treesitter.start)] false)
      opts #(if (= (type $) :string)
                {:command $ : group :pattern $2}
                {:callback $ : group :pattern $2})
      au #(vim.api.nvim_create_autocmd $ (opts $2 $3))]
  (au :VimEnter :CdProjRoot)
  (au :VimResized "wincmd =")
  (au :TextYankPost #(vim.hl.on_yank {:timeout 450}))
  (au :TermOpen :startinsert)
  (au :TermClose #(if ($.file:find :/Downloads/) (vim.cmd.quit)))
  (au :FileType "wincmd L" :help)
  (au :FileType "setl spell spl=en_us" "gitcommit,asciidoc,markdown")
  (au :FileType "setl ts=2 sw=2 sts=2 fdls=0" "lua,vim,zsh,sh")
  (au :FileType "setl ts=4 sw=4 noet cole=1" :go)
  (au :FileType ts-start)
  (au :BufEnter :LastWindow)
  (au :BufEnter "setl ft=nginx" :nginx/*)
  (au :BufEnter "setl ft=risor" :*.risor)
  (au :BufReadPost :JumpToLastLocation)
  (au :BufWritePre "TrimTrailingSpace | TrimTrailingBlankLines"))
